use Mojolicious::Lite;

use FindBin;
BEGIN { unshift @INC, "$FindBin::Bin/lib" }

use File::Basename;
use File::Spec::Functions qw(catfile);

$ENV{MOJO_LISTEN} = 'http://*:8080' if $ENV{MORBO_REV};
plugin 'Config' => {default => {hypnotoad => {listen=>['http://*:8080']}}};

plugin 'Nginx';

die "Router base not defined\n" unless my $base = $ENV{ROUTER_BASE} || app->config->{base};
my $home = dirname app->home;
opendir(my $dh, $home);
foreach ( grep { !/\brouter\b/ && !/^\./ } readdir($dh) ) {
  my $app;
  $app = catfile $home, $_, 'lite_app';
  $app = catfile $home, $_, 'script', $_ unless $app && -f $app;
  if ( $app && -f $app ) {
    my $host = join('.', lc($_), $base);
    plugin Mount => {$host => $app};
    app->log->info("Mounted $host => $app");
  }
}
closedir $dh;

app->start;
